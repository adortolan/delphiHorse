unit backend.Routers.Genres;

interface

uses
  System.JSON,
  Horse,
  Horse.Jhonson,
  Horse.CORS,
  backend.Controller;

  procedure Registry;

implementation

uses
  System.Classes,
  backend.Controller.Interfaces,
  System.SysUtils,
  Horse.Paginate,
  backend.Model.Entity.Genres,
  backend.Utils;

procedure Registry;
begin
  THorse
  .Use(Paginate)
  .Use(Jhonson)
  .Use(CORS)

  .Get('/Genres',
    procedure(Req: THorseRequest; Res: THorseResponse; Next: TProc)
    var
      iController: iControllerEntity<TGenres>;
    begin
      iController := TController.New.Genres;
      iController.This
        .DAO
          .SQL
            .Where(TServerUtils.New.LikeFind(Req))
            .OrderBy(TServerUtils.New.OrderByFind(Req))
          .&End
        .Find(False);

      Res.Send<TJsonArray>(iController.This.DataSetAsJsonArray);
    end
  )

  .Get('/Genres/:ID',
    procedure(Req: THorseRequest; Res: THorseResponse; Next: TProc)
    var
      iController: iControllerEntity<TGenres>;
    begin
      iController := TController.New.Genres;
      iController.This
        .DAO
          .SQL
            .Where('ID = ' + QuotedStr(Req.Params['ID']))
          .&End
        .Find(False);

        Res.Send<TJSONArray>(iController.This.DataSetAsJsonArray);
    end
  )

  .Post('/Genres',
    procedure(Req: THorseRequest; Res: THorseResponse; Next: TProc)
    var
      vBody: TJSONObject;
      aGuuid: string;
    begin
      vBody := TJSONObject.ParseJSONValue(Req.Body) as TJSONObject;

      try
        if not vBody.TryGetValue<String>('id', aGuuid) then
          vBody.AddPair('id', TServerUtils.New.AdjustGuuid(TGuid.NewGuid.ToString()));
        TController.New.Genres.This.Insert(vBody);
        Res.Status(200).Send<TJSONObject>(vBody);
      except
        Res.Status(500).Send('');
      end;
    end
  )

  .Put('/Genres/:ID',
    procedure(Req: THorseRequest; Res: THorseResponse; Next: TProc)
    var
      vBody: TJSONObject;
      aGuuid: string;
    begin
      vBody := TJSONObject.ParseJSONValue(Req.Body) as TJSONObject;

      try
        if not vBody.TryGetValue<string>('id', aGuuid) then
          vBody.AddPair('id', Req.Params['ID']);
        TController.New.Genres.This.Update(vBody);
        Res.Status(200).Send<TJSONObject>(vBody);
      except
        Res.Status(500).Send('');
      end;
    end
  )

  .Delete('/Genres/:ID',
    procedure(Req: THorseRequest; Res: THorseResponse; Next: TProc)
    begin
      try
        TController.New.Genres.This.Delete('id = ' + QuotedStr(Req.Params['ID']));
        Res.Status(200).Send('');
      except
        Res.Status(500).Send('');
      end
    end

  )

end;

end.
